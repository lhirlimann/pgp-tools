#!/usr/bin/perl

# $Id$

# small script to show in an intuitive way who signed which of your user ids
#
# Copyright (c) 2004 Uli Martens <uli@youam.net>
# Copyright (c) 2005 Peter Palfrader <peter@palfrader.org>
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions
# are met:
# 1. Redistributions of source code must retain the above copyright
#    notice, this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright
#    notice, this list of conditions and the following disclaimer in the
#    documentation and/or other materials provided with the distribution.
# 3. The name of the author may not be used to endorse or promote products
#    derived from this software without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
# IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
# OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
# IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
# INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
# NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
# DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
# THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
# THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

use strict;
use warnings;
use English;

my $key=shift @ARGV;
unless (defined $key) {
	die "Usage: $PROGRAM_NAME <keyid>\n";
}

open SIGS, "gpg --fixed-list-mode --with-colons --list-sigs $key 2>/dev/null |"
	or die "can't get gpg listing";

my $uid = "";
my @uids;
my %sigs;
my %rev;
my %ids;
my %unknownID;
my $longkey;
while (<SIGS>) {
	if ( m/^uid:.:[^:]*:[^:]*:[^:]*:[^:]*:[^:]*:[^:]*:[^:]*:([:^]*):$/ ) {
		$uid = $1;
		push @uids, $1;
		next;
	}
	if ( m/^sig:[^:]*:[^:]*:[^:]*:([0-9A-F]*):[^:]*:[^:]*:[^:]*:[^:]*:([^:]*):[^:]*:$/ ) {
		$2 eq "[User id not found]" ? $unknownID{$1} = $1 : $ids{$2} = $1;
		$sigs{$1}->{$uid} = "x" unless defined $sigs{$1}->{$uid};
		next;
	}
	if ( m/^rev:[^:]*:[^:]*:[^:]*:([0-9A-F]*):[^:]*:[^:]*:[^:]*:[^:]*:([:^]*):[^:]*:$/ ) {
		$rev{$uid} = "x" if ($longkey eq $1);
		$sigs{$1}->{$uid} = "R";
		next;
	}
	if ( m/^uat:.:[^:]*:[^:]*:[^:]*:[^:]*:[^:]*:[^:]*:[^:]*:([:^]*):$/ ) {
		$uid = "Photo ID";
		push @uids, $uid;
		next;
	}
	if ( m/^pub:[^:]*:[^:]*:[^:]*:([^:]*):[^:]*:[^:]*:[^:]*:[^:]*:([:^]*):$/ ) {
		$longkey = $1;
		next;
	}
	last if ( m/^(sub):/ );
	next if ( m/^(tru):/ );
	print STDERR "hi, i'm a bug. please report me to my owner";
	die "input: $_, key: $key";
}
close SIGS;

# XXX: Add an option for this
my $c = 0;
@uids = grep { ! defined $rev{$uids[$c++]} } @uids;

my $n = scalar @uids -1;
for ( my $a=0; $a <= $n; $a++ ) {
	printf "|  " x ($a)
	     . "+--"
	     . "---" x ($n-$a)
	     . (defined $rev{$uids[$a]} ? "R" : " ")
	     . "%2i $uids[$a]\n", $a+1;
}

for ( my $a=0; $a <= $n; $a++ ) {
	printf "%-2i ", $a+1;
}
print "\n";

for my $id (sort keys %ids) {
	print((defined $sigs{$ids{$id}}->{$_} ? $sigs{$ids{$id}}->{$_} : " ") . "  ") for (@uids);
	print $ids{$id}." $id\n";
}
for my $id (sort keys %unknownID) {
	print((defined $sigs{$id}->{$_} ? $sigs{$id}->{$_} : " ") . "  ") for (@uids);
	print "$id [User id not found]\n";
}
